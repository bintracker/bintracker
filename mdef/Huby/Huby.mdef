(mdal-definition
 mdef-version: 2 engine-version: 1.0 target: spectrum48
 description: "A simple 2 channel pin pulse (PFM) engine in less than 100 bytes.
 By Shiru 2011, 2013."
 default-origin: #x8000

 commands: ((command id: BPM bits: 16 type: uint default: 140
		     flags: (is-bpm))
	    (command id: NOTE bits: 8 type: ukey
		     flags: (enable-modifiers use-last-set is-note)
		     keys: (make-inverse-dividers 118 8 0 -1)
		     default: rest)
	    (command id: DRUM type: trigger default: #f description:
		     "Trigger a click drum. Replaces note on channel 1."
		     flags: (is-percussion)))

 input: ((field from: BPM)
	 (group id: PATTERNS flags: (ordered playable)
		nodes: ((block id: DRUMS nodes: ((field from: DRUM)))
			(clone 2 (block id: CH
					nodes: ((field from: NOTE)))))))

 output: ((asm file: "huby.asm")
	  (comment "song speed")
	  (field bytes: 2 compose: (quotient 1779661 ?BPM))
	  (comment "sequence")
	  (field bytes: 2 compose: (- $sequence_end 8))
	  (order from: PATTERNS layout: shared-numeric-matrix
		 element-size: 1 base-index: 1)
	  (field bytes: 1 compose: 0)
	  (symbol id: sequence_end)
	  (group id: PATTERNS from: PATTERNS nodes:
		 ((block id: CH1 from: (CH1 DRUMS) resize: 8
		  	 nodes: ((repeat bytes: 1
					 compose: (if ??DRUM #x2c ?NOTE1))))
		  (block id: CH2 from: (CH1 DRUMS CH2) resize: 8
			 nodes: ((repeat bytes: 1
					 compose: (if (and (= ?NOTE1 ?NOTE2)
							   (not ??DRUM))
						      0
						      ?NOTE2))))))))
